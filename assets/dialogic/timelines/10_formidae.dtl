if {GameManager.player_inventory.has_item(9)}:
	Um antigo esqueleto de formidae, agora sem nada em mãos.
	if {GameManager.player_inventory.has_item(8)} and ({GameManager.player_inventory.has_item(7)} == false):
		Você olha para o corpo do soldado, e pensa em arrancar uma de suas garras para o terminal biométrico.
		nuzzle: Hm... Não acho que este é o esqueleto certo.
		nuzzle: Um formidae que poderia ter acesso àquela sala provavelmente seria de um cunho mais científico.
		nuzzle: Este parece mais um militar.
else:
	Você encontra o corpo de um soldado formidae.
	nuzzle: Como ele entrou aqui...?
	Você olha pra cima e vê um duto de ventilação quebrado.
	Você vê que o esqueleto segura firmemente um... módulo eletrônico em suas mãos?
	- Pegar o módulo.
		Você abre a mão do esqueleto gentilmente, e pega o módulo.
		O tipo do conector parece compatível com o seu relicário, na porta de entrada de dados.
		[signal arg="skeleton_moved"]
		nuzzle: "Ah!"
		Você leva um susto quando o esqueleto se move, quase caindo sobre você.
		Ao conectar o módulo no seu relicário, a tela se acende, e em poucos instantes você vê uma mensagem.
		nuzzle: "Módulo de linguagem adicionado."
		Seu relicário agora era capaz de traduzir palavras e frases em formidian mais complexas.
	- Não pegar o módulo.
		if {GameManager.player_inventory.has_item(7)}:
			Talvez não seria bom mexer com mais restos mortais do que você já mexeu.
		else:
			Talvez não seria bom mexer com restos mortais.